buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.7'
	id "net.minecraftforge.gradle.forge" version "2.0.2"
}

apply plugin: 'maven-publish'

configurations {
    shade
    compile.extendsFrom shade
}

def username = "${mod_name}"
if (project.hasProperty('dev_username'))
    username = "${dev_username}"

def build_number = 'CUSTOM'
if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

group = package_group
archivesBaseName = mod_name
version = "${mc_version}-${mod_version}-${build_number}"

repositories {
    maven { url "http://tehnut.info/maven" }
    maven { url "http://dvs1.progwml6.com/files/maven" }
}

dependencies {
    // TehNut Maven
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.13-B26_1.11"
    deobfCompile "info.amerifrance.guideapi:Guide-API:1.11-2.1.0-51"
    shade "tehnut.lib:LendingLibrary:1.11.2-2.0.0-19"

    // Prog Maven
    deobfCompile "mezz.jei:jei_1.11.2:4.2.10.248"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    replaceIn "Nomagi.java"
    replace "@VERSION@", project.version

    clientRunArgs += "--username=${username}"

    if (project.hasProperty('mappings_version'))
        mappings = project.mappings_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

reobf {
    jar {
        extraLines "PK: tehnut/lib info/sleeplessacorn/nomagi/lib"
    }
}

jar {
    classifier = ''
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest.mainAttributes(
            "Built-By": System.getProperty('user.name'),
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Built-on": "${mc_version}-${forge_version}"
    )
}

tasks.build.dependsOn sourceJar

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

static String getChangelogText() {
    def changelogFile = new File('changelog.txt')
    if (!changelogFile.exists())
        return ""
    String str = ''
    String separator = '---'
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (lineCount < 3) {
            lineCount++
            if (it.startsWith(separator)) {
                return
            }
        }
        if (!it.startsWith(separator)) {
            str += "$it" + (lineCount < 3 ? ':\n\n' : '\n')
            return
        }
        done = true // once we go past the first version block, parse no more
    }
    return str
}

curseforge {
    if (project.hasProperty('curse_key_TehNut'))
        apiKey = project.curse_key_TehNut

    project {
        id = "${curse_id}"
		changelog = getChangelogText()
        releaseType = 'beta'

        addArtifact sourceJar
    }
}